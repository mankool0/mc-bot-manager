plugins {
    id 'fabric-loom' version '1.11-SNAPSHOT'
    id 'maven-publish'
    id 'com.google.protobuf' version '0.9.5'
}

// Load local deployment configuration if it exists
if (file('deploy.gradle').exists()) {
    apply from: 'deploy.gradle'
}

version = project.mod_version
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

loom {
    splitEnvironmentSourceSets()

    mods {
        "mc-bot-client" {
            sourceSet sourceSets.main
            sourceSet sourceSets.client
        }
    }
}

fabricApi {
    configureDataGeneration {
        client = true
    }
}

sourceSets {
    main {
        proto {
            //noinspection GroovyAssignabilityCheck
            srcDir '../proto'
        }
    }
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${project.protobuf_version}"
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {}
            }
        }
    }
}

// Fix duplicate proto files issue
tasks.withType(Copy).configureEach {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

repositories {
    // Add repositories to retrieve artifacts from in here.
    // You should only use this when depending on other mods because
    // Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
    // See https://docs.gradle.org/current/userguide/declaring_repositories.html
    // for more information about repositories.
    mavenCentral()

    // Meteor Client maven repository
    maven {
        name = "Meteor"
        url = "https://maven.meteordev.org/releases"
    }
    maven {
        name = "Meteor Snapshots"
        url = "https://maven.meteordev.org/snapshots"
    }

    maven {
        name = "babbaj-repo"
        url = "https://babbaj.github.io/maven/"
    }
}

dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    // Meteor Client - utility mod
    modImplementation "meteordevelopment:meteor-client:${project.meteor_version}"

    // Baritone (https://github.com/MeteorDevelopment/baritone)
    modImplementation "meteordevelopment:baritone:${project.baritone_version}"
    include "meteordevelopment:baritone:${project.baritone_version}"

    // Baritone's nether-pathfinder dependency (from babbaj-repo)
    modImplementation "dev.babbaj:nether-pathfinder:1.6"

    // Protocol Buffers - using implementation for compile, include for runtime
    implementation "com.google.protobuf:protobuf-java:${project.protobuf_version}"
    include "com.google.protobuf:protobuf-java:${project.protobuf_version}"

    // JNA for Windows named pipes
    implementation "net.java.dev.jna:jna:${project.jna_version}"
    implementation "net.java.dev.jna:jna-platform:${project.jna_version}"
    include "net.java.dev.jna:jna:${project.jna_version}"
    include "net.java.dev.jna:jna-platform:${project.jna_version}"

    // junixsocket for Unix domain sockets
    implementation "com.kohlschutter.junixsocket:junixsocket-core:${project.junixsocket_version}"
    implementation "com.kohlschutter.junixsocket:junixsocket-common:${project.junixsocket_version}"
    implementation "com.kohlschutter.junixsocket:junixsocket-native-common:${project.junixsocket_version}"
    include "com.kohlschutter.junixsocket:junixsocket-core:${project.junixsocket_version}"
    include "com.kohlschutter.junixsocket:junixsocket-common:${project.junixsocket_version}"
    include "com.kohlschutter.junixsocket:junixsocket-native-common:${project.junixsocket_version}"
}

processResources {
    inputs.property "version", project.version
    inputs.property "minecraft_version", project.minecraft_version
    inputs.property "loader_version", project.loader_version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": project.version,
                "minecraft_version": project.minecraft_version,
                "loader_version": project.loader_version
    }
}

def targetJavaVersion = 21
tasks.withType(JavaCompile).configureEach {
    // ensure that the encoding is set to UTF-8, no matter what the system default is
    // this fixes some edge cases with special characters not displaying correctly
    // see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
    // If Javadoc is generated, this must be specified in that task too.
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release.set(targetJavaVersion)
    }
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

// configure the maven publication
publishing {
    publications {
        create("mavenJava", MavenPublication) {
            artifactId = project.archives_base_name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
        // Notice: This block does NOT have the same function as the block in the top level.
        // The repositories here will be used for publishing your artifact, not for
        // retrieving dependencies.
    }
}
